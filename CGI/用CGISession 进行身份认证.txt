用 CGI::Session 进行身份认证
身份验证通常使用 cookie。但是把用户名和密码放在客户端的 cookie 里， 总是会让客户担心受怕。使用会话应该是一个比较合适的方法。 这里介绍一个用 CGI::Session 进行身份验证的例子。 具体怎样试试就知道了。有空再写详细一些。有其它问题欢迎写信到邮箱， 一起讨论。我也是刚用。  
  
登录
  
#!/usr/bin/perl
# login.cgi ---
# Last modify Time-stamp: <2005-09-11 20:01:34 Wenbin Ye>
  
#===========================================================
#       Version: v 0.0 2005/09/11 10:59:02
#        Author: Wenbin Ye <wenbinye@163.com>
#
#   Description: Web Login
#===========================================================
  
use strict;
use warnings;
use CGI::Pretty ;
use CGI::Carp qw(fatalsToBrowser);
use CGI::Session;
  
my $debug = 0;
my $cgi = new CGI;
my ($sid, $session);
my ($user, $passwd);
my $cookie_name = "LOGIN_CGISESSID";
$user = $cgi->param("user");
$passwd = $cgi->param("passwd");
$sid = $cgi->cookie($cookie_name);
my $oldsid = $sid;
# if post user, destroy old session and create new session
# and store user and password.
if ($user) {
     if ($sid) {
         $session = new CGI::Session(
             "driver:File", $sid, {Directory=>'./tmp'},
         );
         $session->delete;
     }
     $session = new CGI::Session(
         "driver:File", undef, {Directory=>'./tmp'},
     );
     $session->param('user', $user);
     $session->param('passwd', $passwd);
}
# if not post user, read the session or create new session
else {
     $sid = $sid || undef;
     $session = new CGI::Session(
         "driver:File", $sid, {Directory=>'./tmp'},
     );
     $user = $session->param('user');
     $passwd = $session->param('passwd');
}
$session->expire("+1m");
$sid = $session->id();
my $cookie = $cgi->cookie($cookie_name=>$sid);
print $cgi->header(-charset=>"gb2312",
                -cookie=>$cookie);
  
print $cgi->start_html(
     -lang=>'zh',
     -title => "Login",
     -head => $cgi->meta( {
             -http_equiv=>"Content-Type",
             -content=>'text/html; charset=gb2312',
             },
         ),
     -style=>{ -src=>"" },
    );
  
if ($debug) {
     print $cgi->h4("Old sid: $oldsid, and new sid: $sid");
}
print $cgi->h4("登录");
print $cgi->start_form;
print $cgi->table(
     $cgi->Tr(
         $cgi->td(
             {class=>"login"},"用户名"
                 ),
         $cgi->td(
             $cgi->textfield(
                 -name=>"user",
                 -size=>"10"
                     ),
             ),
         ),
     $cgi->Tr(
         $cgi->td(
             {class=>"login"},"密码"
                 ),
         $cgi->td(
             $cgi->password_field(
                 -name=>"passwd",
                 -size=>"10"
                     ),
             ),
         ),
);
print $cgi->submit("login","登录");
print $cgi->submit("new", "注册");
print $cgi->end_form;
  
# test login here
if ($user) {
     print $cgi->h3("You are $user, and password is $passwd");
     print $cgi->start_form(
         {
             "action"=>"logout.cgi"
         }
     );
     print $cgi->submit("logout", "退出");
     print $cgi->end_form;
} else {
     print $cgi->h3("Please login!");
}
print $cgi->a( {href=>"count.cgi"}, "到处看看！");
print $cgi->end_html;
退出
  
#!/usr/bin/perl
# logout.cgi ---
# Last modify Time-stamp: <2005-09-11 19:52:15 Wenbin Ye>
  
#===========================================================
#       Version: v 0.0 2005/09/11 10:59:02
#        Author: Wenbin Ye <wenbinye@163.com>
#
#   Description: Web Login
#===========================================================
  
use strict;
use warnings;
use CGI::Pretty ;
use CGI::Carp qw(fatalsToBrowser);
use CGI::Session;
  
my $cgi = new CGI;
my $session;
my $cookie_name = "LOGIN_CGISESSID";
my $sid = $cgi->cookie($cookie_name);
if ($sid) {
     $session = new CGI::Session(
         "driver:File", $sid, {Directory=>'./tmp'},
     );
     $session->delete;
}
  
print $cgi->header(-charset=>"gb2312");
print $cgi->start_html(
     -lang=>'zh',
     -title => "Logout",
     -head => $cgi->meta( {
             -http_equiv=>"Content-Type",
             -content=>'text/html; charset=gb2312',
             },
         ),
     -style=>{ -src=>"" },
);
  
print $cgi->h4("成功退出！");
print $cgi->a( {href=>"login.cgi"}, "再来一次！");
一个小测试文件
  
#!/usr/bin/perl
# count.cgi ---
# Last modify Time-stamp: <2005-09-11 20:22:29 Wenbin Ye>
  
#===========================================================
#       Version: v 0.0 2005/09/11 09:41:34
#        Author: Wenbin Ye <wenbinye@163.com>
#
#   Description: session test
#===========================================================
  
use strict;
use warnings;
use CGI::Session;
use CGI::Pretty;
  
my $debug = 0;
my $cgi = new CGI;
my $cookie_name = "CNT_CGISESSID";
my $sid = $cgi->cookie($cookie_name) || undef;
my $oldsid = $sid;
my $session = new CGI::Session(
     "driver:File", $sid, {Directory=>'./tmp'},
);
$session->expire("+1m");
$sid = $session->id();
my $cookie = $cgi->cookie($cookie_name=>$sid);
print $cgi->header(-cookie=>$cookie, -charset=>'gb2312');
  
my $count = $session->param('count');
unless ($count) {
     $count = 1;
} else {
     $count++;
}
$session->param('count', $count);
print $cgi->start_html(
     -lang=>'zh',
     -title => "Test session",
     -head => $cgi->meta( {
             -http_equiv=>"Content-Type",
             -content=>'text/html; charset=gb2312',
             },
         ),
    );
  
if ($debug) {
     print $cgi->h4("Old sid: $oldsid, and new sid: $sid");
}
print $cgi->p("count: $count");
print $cgi->a( {href=>"login.cgi"}, "到处看看！");
print $cgi->end_html;
